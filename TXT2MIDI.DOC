		******************************
			TXT2MIDI v1.13

		text mnemonics to midi binaries
		      by Guenter Nagler
			    1995 
		  (info@gnmidi.com)
		******************************

[0] FEATURES
    + compiles text into midi
    + modify existing midi files with help of MIDI2TXT
    + can be used to add text to midi files (copyright notice, lyric, ...)
    + copy and loop patterns
    + named GM-program names
    + additional text commands for Korg i2/i3 (e.g. bankdrum)
    + C-like comments // and /* ... */
    + mute and solo some channels 
    + handles format 0 (all channels in a track) and format 1 midi files
      (separate tracks)
    + tempo can be specified midi like (ticks/minute) or in beats/minute 
    + sysex events (including GMReset, GSReset, GSExit)
    + meta events (popular or unknown events)
    + direct events (midi command bytes)
    + compressing output by ommiting redunant midi command bytes

[1] BACKGROUND
After writing the pendant MIDI2TXT program it was obvious to program
a compiler to translate the text back to a midi binary. I used it to sequence 
some music titles. When I bought my music workstation (including a more
powerful sequencer) my use of MIDI2TXT/TXT2MIDI changed into an
updating environment to make less compatible midi files full compatible.
Another use possibility came up when I copied some excellent midi
binaries from news://alt.binaries.sounds.midi . I converted one with
MIDI2TXT into text, commented all commands that played notes in leading
track and converted it back into another midi file (TXT2MIDI). The new 
midi file played the full background of the music while I was able to 
learn playing the lead voice for my own pleasure.

[2] FILES DESCRIPTION

TXT2MIDI.EXE.........compiler 
TXT2MIDI.DOC.........this file, showing usage of TXT2MIDI.EXE
only TXT2MIDI.EXE is required to run program

[3] COPYRIGHT

TXT2MIDI (c) 1995 was created by Guenter Nagler.

TXT2MIDI is free and may be used as you wish with this one exception:

    You may NOT charge any fee or derive any profit for distribution
    of TXT2MIDI.  Thus, you may NOT sell or bundle TXT2MIDI with any
    product in a retail environment (shareware disk distribution, CD-ROM,
    etc.) without permission of the author.

You may give TXT2MIDI to your friends, upload it to a BBS, or ftp it to
another internet site, as long as you don't charge anything for it.
Please send me a notice if you locate the program on a public BBS.

Distributing modified programs or own programs that use parts of this 
program requires permission of author!

[4] DISCLAIMER

TXT2MIDI was designed to compile text file that are generated by
MIDI2TXT. So I give no guarantees of the compiler results, especially 
with text files that are not generated by MIDI2TXT.
If you create an error free text file that can not be compiled
correctly, please send a sample file to info@gnmidi.com . 

Use TXT2MIDI at your own risk.  Anything you do with TXT2MIDI is your
responsibility, and not the author's.  Any damage caused to any person,
computer, software, hardware, company, or business by running TXT2MIDI 
is your responsibility, and the author will not be liable.

If you don't understand these terms, or are not sure of something, or
are afraid something bad might come of using TXT2MIDI, don't  use  it!
You are here forewarned.

[5] INSTALLATION

[MSDOS]
Simply copy TXT2MIDI.EXE in a directory that is in your path.

[6] USAGE

usage: TXT2MIDI [-programs] textfile.txt midifile.mid

TXT2MIDI translates the textfile.txt which contains
a text representation of a midi file into binary.mid that should contain
a valid midi file if the compiler does not complain about errors or warnings.

When starting TXT2MIDI with option -programs then a list of the usable 
program names in program statements is printed. Instead of names also 
numbers 0-127 can be used.

Warning:
  Do not expect that MIDI2TXT + TXT2MIDI will produce a copy of the original
  file! MIDI allows to encode its commands in different ways without 
  changing the semantic of the notes (It is allowed to compress the 
  midi file by ommiting redunant command bytes, but it is no duty).

[7] GRAMMAR

Extended BNF rules:
-----------------------------------
symbol ::= expr ;            rule for symbol
expr can be:
expr*                        optional list of expr's
expr+                        repetition of expr's (at least 1)
[expr]                       optional expr  (0 or 1 occurence)
expr1 expr2 ... exprN        sequence of expr1 ... exprN (in this order)
expr1|expr2|...|exprN        alternatives between expr1...exprN (choose one) 
(expr)                       expr itself for grouping (e.g. ("+"|"-")*
"mthd" "("                   keywords and operators (case sensitive, use 
			     without " characters)
// text                      comment until next line
literal   ::=   characters enclosed in "..."  e.g. "Track 1"
		(special characters can be escaped by preceding \
		e.g. "\""  is " character itself

lexical symbols (in E-BNF):
---------------------------------------

digit     ::=   "0"|"1"|"2"|"3"|"4"|"5"|"6"|"7"|"8"|"9" ;
hexdigit  ::=   digit|"A"|"B"|"C"|"D"|"E"|"F"|"a"|"b"|"c"|"d"|"e"|"f" ;
decnumber ::=   digit+ ;
hexnumber ::=   "0" ("x"|"X") hexdigit+ 
		| "$" hexdigit+
		;
number    ::=   decnumber | hexnumber ;
floatnumber ::= decnumber | decnumber "." decnumber ;
notekey   ::=   "C"|"D"|"E"|"F"|"G"|"A"|"H"|"B"
	      | "c"|"d"|"e"|"f"|"g"|"a"|"h"|"b" ;
		    // notekey "h" used in German language is equal to
		    // notekey "b"

notename  ::=   notekey ["#" | "is" | "b" | "es"] decnumber ;
		    // only legal notes as known in music theory are allowed
		    // suffixes "is" and "es" are used in German language
		    // "is" is equal to "#"
		    // "es" is equal to "b"
		    // exceptions rules (in German language):
		    // 	As is equal to Ab
		    // 	Es is equal to Eb
		    // 	Use F instead of Eis
		    //  Use C instead of His
		    // 	Use As instead of Aes
		    //  Use Es instead of Ees
		    //  Use H instead of Ces
		    //  Use E instead of Fes

note      ::=   notename | number ;

programname ::= 
  // korg i2/i3 only:
  "Dr1"|"Dr2"|"Dr3"|"Dr4"|"Dr5"|"Dr6"|"Dr7"|"Dr8"| 
  // new program names
  "GrandPno"|"BritePno"|"El.Grand"|"HnkyTonk"|"ElPiano1"|"ElPiano2"|
  "Harpsich"| "Clavi."|"Celesta"|"Glocken"|"MusicBox"|"Vibes"|"Marimba"|
  "Xylophon"|"TubulBel"|"Dulcimer"|"DrawOrgn"|"PercOrgn"|"RockOrgn"|"ChrcOrgn"|
  "ReedOrgn"|"Acordion"|"Harmnica"|"TangoAcd"|"NylonGtr"|"SteelGtr"|"JazzGtr"|
  "CleanGtr"|"MuteGtr"|"Ovrdrive"|"Distortd"|"Harmnics"|"WoodBass"|"FngrBass"|
  "PickBass"|"Fretless"|"SlapBas1"|"SlapBas2"|"SynBass1"|"SynBass2"|"Violin"|
  "Viola"|"Cello"|"Contra"|"TremStrg"|"Pizzicto"|"Harp"|"Timpani"|"Ensmble1"|
  "Ensmble2"|"SynStrg1"|"SynStrg2"|"AahChoir"|"OohChoir"|"SynChoir"|"OrchHit"|
  "Trumpet"|"Trombone"|"Tuba"|"MuteTrum"|"FrenchHr"|"BrasSect"|"SynBras1"|
  "SynBras2"|"SprnoSax"|"AltoSax"|"TenorSax"|"BariSax"|"Oboe"|"EnglHorn"|
  "Bassoon"|"Clarinet"|"Piccolo"|"Flute"|"Recorder"|"PanFlute"|"Bottle"|
  "Shakhchi"|"Whistle"|"Ocarina"|"SquareLd"|"SawLd"|"CaliopLd"|"ChiffLd"|
  "CharanLd"|"VoiceLd"|"FifthLd"|"Bass&Ld"|"NewAgePd"|"WarmPd"|"PolySyPd"|
  "ChoirPd"|"BowedPd"|"MetalPd"|"HaloPd"|"SweepPd"|"Rain"|"SoundTrk"|
  "Crystal"|"Atmosphr"|"Bright"|"Goblin"|"Echoes"|"SciFi"|"Sitar"|"Banjo"|
  "Shamisen"|"Koto"|"Kalimba"|"Bagpipe"|"Fiddle"|"Shanai"|"TnklBell"|
  "Agogo"|"StlDrum"|"WoodBlok"|"TaikoDrm"|"MelodTom"|"SynthTom"|"RevCymbl"|
  "FretNoiz"|"BrthNoiz"|"Seashore"|"Tweet"|"Telphone"|"Helicptr"|"Applause"|
  "Gunshot"|
// old general midi programs (GM):
  "Piano"| "BritePiano"| "HammerPiano"| "HonkeyTonk"| "NewTines"| "DigiPiano"| "Harpsicord"| "Clav"|
  "Celesta"| "Glocken"| "MusicBox"| "Vibes"| "Marimba"| "Xylophon"| "Tubular"| "Santur"|
  "FullOrgan"| "PercOrgan"| "BX-3Organ"| "ChurchPipe"| "Positive"| "Musette"| "Harmonica"| "Tango"|
  "ClassicGtr"| "A.Guitar"| "JazzGuitar"| "CleanGtr"| "MuteGuitar"| "OverDrive"| "DistGuitar"| "RockMonics"|
  "JazzBass"| "DeepBass"| "PickBass"| "FretLess"| "SlapBass1"| "SlapBass2"| "SynthBass1"| "SynthBass2"|
  "Violin"| "Viola"| "Cello"| "ContraBass"| "TremoloStr"| "Pizzicato"| "Harp"| "Timpani"|
  "Marcato"| "SlowString"| "AnalogPad"| "StringPad"| "Choir"| "DooVoice"| "Voices"| "OrchHit"|
  "Trumpet"| "Trombone"| "Tuba"| "MutedTrumpet"| "FrenchHorn"| "Brass"| "SynBrass1"| "SynBrass2"|
  "SopranoSax"| "AltoSax"| "TenorSax"| "BariSax"| "SweetOboe"| "EnglishHorn"| "BasoonOboe"| "Clarinet"|
  "Piccolo"| "Flute"| "Recorder"| "PanFlute"| "Bottle"| "Shakuhachi"|"Whistle"| "Ocarina"|
  "SquareWave"| "SawWave"| "SynCalinope"| "SynChiff"| "Charang"| "AirChorus"| "Rezzo4ths"| "Bass&Lead"|
  "Fantasia"| "WarmPad"| "PolyPad"| "GhostPad"| "BowedGlas"| "MetalPad"| "HaloPad"| "Sweep"|
  "IceRain"| "SoundTrack"| "Crystal"| "Atmosphere"| "Brightness"| "Goblin"| "EchoDrop"| "StarTheme"|
  "Sitar"| "Banjo"| "Shamisen"| "Koto"| "Kalimba"|"Scotland"|"Fiddle"| "Shanai"|
  "MetalBell"| "Agogo"| "SteelDrums"| "Woodblock"| "Taiko"| "Tom"| "SynthTom"| "RevCymbal"|
  "FretNoise"| "NoiseChiff"| "Seashore"| "Birds"| "Telephone"| "Helicopter"| "Stadium!!"| "GunShot"
  ;

Grammar in extended BNF
-----------------------------------

midifile ::= midisong
  ;

midisong ::= songoption* midihead songoption* miditrack+
  ;

songoption ::= "mute" channel+     // ignore these channels
  | "solo" channel+                // use these channels only
  ;

channel ::= number                 // only 1-16 are valid channels
  ;                                // channel 10 should be used for drums

midihead ::= "mthd" [version] [unit] "end" "mthd"
  ;

version ::= "version" number   // default version: 1
  // currently only versions 0-2 are allowed
  // version 0 = single multichannel track
  // version 1 = some singlechannel tracks playing together
  // version 2 = some multichannel tracks playing one after one

unit ::=  "unit" number              // default unit: 192
  ;

miditrack ::= "mtrk" [ "(" channel ")" ] 
		 event* 
	      "end" "mtrk" 
	      ;

event ::= [ "[" channel "]" ] midievent
  | "velocyon" number    // default is 127
  | "velocyoff" number   // default is 0
  | duration sep         // pause: delay between events
  | "print" sep
  | "transpose" ["+"|"-"] number sep 
  | sep
  | "copy" "part" literal ;         
  | "part" literal 
	event*
    "end" "part" literal 
    ;
  | "loop" number 
	event* 
    "end" "loop"
    ;

sep ::= ";";    

midievent ::=
    "seqnumber" number
  | "text" literal
  | "copyright" literal
  | "trackname" literal
  | "instrument" literal
  | "lyric" literal
  | "prefixchannel" channel  // following sysex or meta event is applied to this channel
  | "prefixport"   number   // following sysex or meta event is applied to this port
  | "smpteofs" number number ":" number ":" number ":" number ":" number
		// SMPTE mode hour:minute:second:frame:fractional_frame
		// mode 0:  24 frames/second
		// mode 1:  25 frames/second
		// mode 2:  30 frames/second allow dropping frames
		// mode 3:  30 frames/second no dropping allowed
  | "tact" number "/" number number number  // tactnom / (2 ^^ tactdenom)  clicks/beat 32th/beat
  | "tempo" number  // microseconds per quarternote
  | "beats" floatnumber  // same as 60.000.000/tempo
			// quarternotes per minute
  | "key" literal      // literal must contain a valid key:
	// "Cmin" "Cmaj" "1bmin" "1bmaj" ... "7bmaj" ... "7#min" "7#maj"
  | "event"            // enter event bytes without change
       bytes	       // no length is added
    "end" "event"
  | "metaevent" number  // metaevent nr. 0-127
       bytes             // length will be automatically added
    "end" "metaevent"    // metaevents are 0xff-codes
  | "sysevent" 	         // sysex event (0xf0)
       bytes  		 // length will be automatically added
    "end" "sysevent"
			// end sysevent code 0xf7 is appended automatically!
  | "gmreset"		// common sysex command to set GM mode on
  | "gsreset"		// common sysex command to set GS mode on (mainly used for Roland, Yamaha)
  | "gsenter"		// same as command gsreset
  | "gsexit"		// common sysex command to set GS mode off (mainly used for Roland, Yamaha)
  | "program" (programname | number | ("A"|"B"|"C"|"D") number
  | "control" number number
  | "hbank" number
  | "lbank" number
  | "banka" 
  | "bankb" 
  | "bankc" 
  | "bankd" 
  | "bankdrum" // bank*:  korg i2/i3 only!
  | "balance" ("left" | "right" | number)   
      // number is a value between 0 and 127: 0 is left and 127 is right
  | "hold" ("on" | "off" | number)
  | "reverb" number
  | "chorus" number
  | "brightness" number
  | "expression" number
  | "pitchmodulation" number
  | "wheel" number
  | "breath" number
  | "foot" number
  | "portamentotime" number
  | "portamento" number
  | "data" number
  | "volume" number
  | "sustain" number
  | "sostenuto" number
  | "softpedal" number
  | "datainc" number
  | "datadec" number
  | "highRPN" number
  | "lowRPN" number
  | "pitchbendrange" number
  | "localon" 
  | "localoff" 
  | "silent" 
  | "allnotesoff" 
  | "omnioff" 
  | "omnion"
  | "monoon"
  | "polyon"
  | "songpos"  number number
  | "songselect" number
  | "tunerequest"
  | "timingclock"
  | "start"
  | "continue"
  | "stop"
  | "activesensing"
  | "polyaftertouch" note number 
  | "aftertouch" number 
  | "pitchbend" number  
  | notename duration number sep  
  | "+" notename (number|"velocyon") sep  
  | "-" notename (number|"velocyoff) sep  
  | "+" number number sep  // note on  with velocity
  | "-" number number sep  // note off with velocity
  ;

duration ::= 
    number             // units as defined in header
  | number "/" number  (tact units, e.g. 3/4)
  ;

program ::= number | programname ;

bytes ::= (number|literal)+ ;

A midi file format documentation can be downloaded by anonymous ftp from
location:
  ftp://ftp.cs.ruu.nl/pub/MIDI/DOC  

Document "midifile" explains all terms used in this grammar.

[8] SAMPLE TEXT

// itsasin.mid                     // comment until end of line
mthd                               // header
  version 1 // several tracks with seperated channels to play all at once
  // 21 tracks
  unit 192 // is 1/4		   // a beat is 192 units (midi resolution)
end mthd                           // end header

mtrk
  tact 4 / 4 24 8		   // typical 4/4 Tact	
  trackname "It's a sin" 	   // first track name is usually song title
  copyright "Pet Shop Boys"	   // copyright meta event
  key "Cmaj"			   // key signature C major (no # or b signs)
  beats 125.00000		   // 125 beats/min (quarter notes per minute)
  48/4;beats 130.00013
end mtrk

mtrk($1)                           // track with channel (hex) 1
  prefixport 0			   // device 0 should play it
  trackname "Bass            "
  program SynthBass1	 	   // a bass sound from General midi instrument set
  volume 127			   //  volume of this channel to maximum
  balance  64			   // center balance (stereo)
  reverb 64			   // reverb effect 
  48/4;+c3 $7F;			   // note C on octave 3 is played with velocity $7F (=127, maximum)
  1/8;-c3 $00;			   // 1/8 pause and then turn C3 note off
  +c3 $7F;
  1/16;-c3 $00;
  ...
end mtrk

mtrk($3)  // track 3
  prefixport 0
  trackname "Sequence        "
  program Bass&Lead
  volume 112
  balance  37
  reverb 64
  48/4;+c4 $69;
  1/16;-c4 $00;
  +g4 $69;
  1/16;-g4 $00;
  ...
end mtrk

mtrk($A)  // track 10
  prefixport 0
  trackname "Drums"
  // no program given => use standard drumkit
  volume 100
  balance  64
  reverb 127		// reverb effect
  chorus 32		// chorus effect
  +c2 $7F;		// each drum is a note  (GM drum note C2 is bass drum)
  1/16;-c2 $00;
  +c2 $7F;
  1/16;-c2 $00;
  ...
end mtrk

[9] SUGGESTIONS / COMMENTS / BUG REPORTS / QUESTIONS

	     WWW:    http://www.gnmidi.com
  		contains all my dos/unix midi programs
	     EMAIL:  info@gnmidi.com

[10] CHANGES
v1.0 to v1.1: 
  * beats floating-point-number is now allowed (e.g. beats 65.5)
    (suggestion by: djeff@cobra.jpl.nasa.gov)

  * optional version field in header (default: is version 1)
  * portability changes for Unix compatibility 
     -  no stricmp/strnicmp calls
     -  no dos.h if not compiling under __MSDOS__
v1.1 to v1.2:
  * keywords now must be followed by whitespace that they are recognized as
    keywords (former: "Trumpets" was separated into words "Trumpet" and "s").
  * invalid characters in the source text now produce "invalid character" 
    error message instead of adding it to the output midi file (major bug, 
    produced invalid midi file).
  * uses less compression (only note on/off commands are compressed, 
    i.e. collects a sequence of these commands to one command)
    to produce safer compatible output for some sequencers.
  * added new keywords:  GMReset, GSReset, GSEnter, GSExit

v1.2 to v1.3:
  * added meta events mnemonics prefixchannel and prefixport
  * added option -version
  * fixed bug that failed to convert note names C-1 .. B-1 into 0..11
  * added documentation for using German language note names

v1.3 to v1.4:
  * fixed bug occuring in use of solo
    where last used channel in a multi channel track is muted
    (didn't close the track and caused invalid midi file)
  * error added if using multiple channels in single channel tracks
    (e.g. track reserved channel 10 and uses other too)
  * fixed bug when using solo,mute in multi channel track: 
    illegal write of delta time for command on mute channel
  * added control commands highRPN and lowRPN and pitchbendrange
  
v1.4 to v1.5:
  * Warning if channel of a track is not initialized
  * fixed bug that disabled "program number" command
    for certain numbers (e.g. 0)
  * forbidding channel numbers out of range 1-16
  * bug fixed in literals converting characters written hex (e.g. \x82 )
  * added command XGreset

v1.5 to v1.6:
  * long events (sysevent, metaevent, event) now don't require
    length information from author, the length is automatically computed
    and inserted when writing the event.
  * SMPTE offset syntax changed, using two values mode, hour instead of
    combined value

v1.6 to v1.7:
  * allow sysex and meta events up to 16000 bytes length

v1.7 to v1.8:
  * changed syntax slightly to allow decimal note numbers instead of note names
    e.g. +59 127;30;-59 127;
   this was necessary because option -dec of midi2txt writes notes in this way.

v1.8 to v1.9:
  * changes that it runs in Win32 and can be called more than one time within a program

v1.9 to v1.10:
  * tact error checks at '.' removed, it is not very useful

v1.10 to v1.11:
  * accept midi file without tracks (only header)

v1.11 to v1.12:
  *  documentation updated, new samples

v1.12 to v1.13:
  * adapted program names for compatibility with other utilities
  * accept old program names for backward compatibility
  * added option -programs that prints the list of program names that can be used 
  * added interpretation of comment following the beats command which contains the 
    precise tempo value e.g. beats 90.70021 /* 661520 microsec/beat */
    this comment is generated by midi2txt, if the value is not similiar to the 
    value calculated from beats/bin then comment is ignored
    
